---
title: Display maior, n√∫meros maiores
date: 2017-11-05T18:18:55-02:00
featuredImage: /images/placarduino-big-numbers-lcd.jpg
tags: [arduino, eletronica, placarduino]
series: [placarduino]
---

No [√∫ltimo post sobre o projeto do Placarduino][], eu adicionei um display de 4
linhas e 20 colunas, mas n√£o alterei o formato de exibi√ß√£o do placar. Eu reservei
essa mudan√ßa para este post: exibir n√∫meros maiores no placar.

Indo direto ao ponto, o resultado ser√° como a imagem acima.

## A teoria

Os displays LCD utilizam uma matriz de pixels de 8 linhas e 5 colunas para cada
caractere. Os pixels que devem ser ligados s√£o representados por um bit 1 e os
pixels desligados por 0. Cada caractere √© formado por um vetor de 8 bytes, onde
apenas os 5 bits menos significativos de cada byte representam uma linha de pixels.

Por exemplo, a letra "E" √© exibida no display que eu tenho como:

![Representa√ß√£o da letra E no display LCD][]

A representa√ß√£o em bits desse caractere √©:

![Representa√ß√£o em bits da letra E no display LCD][]

O que equivale ao seguinte c√≥digo no Arduino:

```c
byte letraE[8] = {
    B11111,
    B10000,
    B10000,
    B11110,
    B10000,
    B10000,
    B11111,
    B00000
};
```

Essas configura√ß√µes de exibi√ß√£o s√£o salvas na mem√≥ria do controlador do LCD
utilizando o c√≥digo ASCII do caractere como endere√ßo. Por exemplo, o c√≥digo da
letra E na tabela ASCII √© 0x45, ent√£o esse √© o endere√ßo desse caractere na
mem√≥ria.

Para exibir um caractere no display, o microcontrolador (nesse caso, o Atmega328
do Arduino) envia para o controlador do LCD o c√≥digo ASCII do caractere e o
controlador busca na mem√≥ria interna como ele deve ser exibido. Infelizmente
essa mem√≥ria do controlador √© somente leitura. Felizmente o controlador possui
uma outra √°rea de mem√≥ria que pode ser escrita - _pelo menos √© o que eu encontrei
[nesse post][post-engineersgarage]_.

Essa √°rea de mem√≥ria secund√°ria possui 64 bytes. Como cada caractere utiliza 8
bytes, isso d√° 64 / 8 = 8 caracteres. A faixa de endere√ßos dos caracteres que
podem ser configurados √© de 0 a 7, que na tabela ASCII s√£o caracteres de controle
e n√£o tem nenhuma exibi√ß√£o por padr√£o.

Um detalhe importante a ser lembrado √© que os caracteres que est√£o sendo exibidos
no LCD est√£o ligados diretamente a essa mem√≥ria. Ent√£o, se voc√™ reconfigurar um
caractere, todos os caracteres com o mesmo c√≥digo que j√° est√£o no LCD ser√£o
"atualizados" para a nova exibi√ß√£o (descobri isso na pr√°tica).

## Criando os caracteres para exibir n√∫meros grandes

O display LCD exibe caracteres de apenas 8x5 pixels. Mas, com um pouco de
criatividade, √© poss√≠vel combinar v√°rios caracteres de 8x5 para formar letras,
n√∫meros ou at√© s√≠mbolos maiores. No caso do Placarduino, isso ser√° utilizado
apenas para exibir os n√∫meros da pontua√ß√£o em um tamanho maior, ent√£o s√≥ √©
preciso criar os n√∫meros de 0 a 9.

Eu defini que o tamanho de cada n√∫mero seria 3x3 caracteres. Com isso, eu
mantenho uma linha livre para exibir o nome do jogador sobre o placar e tamb√©m
posso exibir dois n√∫meros lado a lado para pontua√ß√µes de at√© 99.

O processo que eu utilizei para criar os caracteres foi bem simples: comecei do
0 e fui criando cada n√∫mero at√© chegar no 9. Criei os caracteres customizados
conforme a necessidade, e no fim acabei utilizando exatamente os 8 dispon√≠veis.

Para criar cada parte do n√∫mero, um site que pode ajudar √© esse [gerador de
caracteres customizados][]. Ele s√≥ edita um caractere por vez, ent√£o para
visualizar o resultado eu criei um sketch separado no Arduino para juntar as
partes e ver o n√∫mero completo.

O resultado disso foi a cria√ß√£o de uma biblioteca LcdBigNumbers dentro do projeto.
Usando a [estrutura do PlatformIO][], a biblioteca ficou bem isolada do resto do
c√≥digo e bem simples de utilizar, apenas com um arquivo `.h` e um `.cpp`.

### Defini√ß√£o da classe

O arquivo de cabe√ßalho da biblioteca ficou localizado em `lib/LcdBigNumbers/LcdBigNumbers.h`.
Esse arquivo √© bem simples e cont√©m a defini√ß√£o de uma classe com um construtor
e tr√™s m√©todos p√∫blicos.

```cpp
// lib/LcdBigNumbers/LcdBigNumbers.h

#ifndef PLACARDUINO_LCD_BIG_NUMBERS_H
#define PLACARDUINO_LCD_BIG_NUMBERS_H

class LiquidCrystal_I2C;

class LcdBigNumbers
{
public:
    LcdBigNumbers(LiquidCrystal_I2C *lcd);

    void init();
    void printNumber(const unsigned long int number, uint8_t column, uint8_t row);
    void printDigit(const uint8_t digit, uint8_t column, uint8_t row);

private:
    LiquidCrystal_I2C *lcd;
};

#endif // PLACARDUINO_LCD_BIG_NUMBERS_H
```

Vendo o construtor, ele recebe um ponteiro para o objeto do LCD. _[Dependency
Injection][]_ no Arduino üòÑ. Infelizmente, n√£o existe uma interface padr√£o entre
as bibliotecas do LiquidCrystal, ent√£o foi necess√°rio utilizar a classe
LiquidCrystal_I2C diretamente.

Al√©m do construtor, existem tamb√©m um m√©todo `init()` que ser√° utilizado para
criar os caracteres na mem√≥ria do controlador do LCD, um m√©todo `printNumber()`
para exibir um n√∫mero em uma posi√ß√£o espec√≠fica do display, e o m√©todo
`printDigit()` que exibe um √∫nico d√≠gito no LCD tamb√©m em uma posi√ß√£o espec√≠fica.

### Configura√ß√£o dos caracteres

O arquivo que possui toda a configura√ß√£o dos caracteres customizados, bem como
a l√≥gica para exibir os n√∫meros maiores compostos de 9 caracteres (3x3) √©
`lib/LcdBigNumbers/LcdBigNumbers.cpp`.

No come√ßo do arquivo s√£o inclu√≠dos os arquivos de cabe√ßalho necess√°rios:

```cpp
#include <inttypes.h>
#include <binary.h>

#include <LiquidCrystal_I2C.h>

#include "LcdBigNumbers.h"
```

Logo depois, eu criei algumas constantes para identificar os caracteres
customizados, al√©m de outros caracteres do pr√≥prio controlador do LCD que foram
utilizados:

```cpp
#define CHAR_TOP_LEFT       0
#define CHAR_TOP_RIGHT      1
#define CHAR_BOTTOM_LEFT    2
#define CHAR_BOTTOM_RIGHT   3
#define CHAR_HALF_TOP       4
#define CHAR_HALF_BOTTOM    5
#define CHAR_TOP_BOTTOM     6
#define CHAR_MIDDLE         7
#define CHAR_EMPTY        254
#define CHAR_FULL         255
```

Esses s√£o os endere√ßos dos caracteres na mem√≥ria do controlador LCD. Os √∫ltimos
dois, `CHAR_EMPTY` e `CHAR_FULL`, j√° existem por padr√£o na mem√≥ria do LCD e n√£o
√© necess√°rio cri√°-los. A defini√ß√£o de cada pixel desses caracteres vem logo
depois, em uma matriz de bytes no formato j√° mencionado, utilizando o endere√ßo
do caractere como √≠ndice do vetor:

```cpp
static uint8_t customChars[8][8] =
{
    // CHAR_TOP_LEFT
    {
        B00001,
        B00111,
        B01111,
        B01111,
        B11111,
        B11111,
        B11111,
        B11111
    },

    // CHAR_TOP_RIGHT
    {
        B10000,
        B11100,
        B11110,
        B11110,
        B11111,
        B11111,
        B11111,
        B11111
    },

    // CHAR_BOTTOM_LEFT
    {
        B11111,
        B11111,
        B11111,
        B11111,
        B01111,
        B01111,
        B00111,
        B00001
    },

    // CHAR_BOTTOM_RIGHT
    {
        B11111,
        B11111,
        B11111,
        B11111,
        B11110,
        B11110,
        B11100,
        B10000
    },

    // CHAR_HALF_TOP
    {
        B11111,
        B11111,
        B11111,
        B11111,
        B00000,
        B00000,
        B00000,
        B00000
    },

    // CHAR_HALF_BOTTOM
    {
        B00000,
        B00000,
        B00000,
        B00000,
        B11111,
        B11111,
        B11111,
        B11111
    },

    // CHAR_TOP_BOTTOM
    {
        B11111,
        B11111,
        B11111,
        B00000,
        B00000,
        B11111,
        B11111,
        B11111
    },

    // CHAR_MIDDLE
    {
        B00000,
        B00000,
        B00000,
        B11111,
        B11111,
        B00000,
        B00000,
        B00000
    }
};
```

Os caracteres que s√£o criados com essas configura√ß√µes s√£o os seguintes:

![Todos os caracteres utilizados na biblioteca LcdBigNumbers][]

Depois de ter esses caracteres criados, cada n√∫mero √© configurado como uma matriz
3x3 com os c√≥digos desses caracteres. Como a matriz tem n√∫meros de 0 a 9, o
pr√≥prio n√∫mero foi utilizado como √≠ndice:

```cpp
static uint8_t numberChars[10][3][3] =
{
    // 0
    {
        { CHAR_TOP_LEFT, CHAR_HALF_TOP, CHAR_TOP_RIGHT },
        { CHAR_FULL, CHAR_EMPTY, CHAR_FULL },
        { CHAR_BOTTOM_LEFT, CHAR_HALF_BOTTOM, CHAR_BOTTOM_RIGHT }
    },

    // 1
    {
        { CHAR_HALF_BOTTOM, CHAR_FULL, CHAR_EMPTY },
        { CHAR_EMPTY, CHAR_FULL, CHAR_EMPTY },
        { CHAR_HALF_BOTTOM, CHAR_FULL, CHAR_HALF_BOTTOM }
    },

    // 2
    {
        { CHAR_HALF_TOP, CHAR_HALF_TOP, CHAR_HALF_BOTTOM },
        { CHAR_HALF_BOTTOM, CHAR_MIDDLE, CHAR_HALF_TOP },
        { CHAR_FULL, CHAR_HALF_BOTTOM, CHAR_HALF_BOTTOM }
    },

    // 3
    {
        { CHAR_HALF_TOP, CHAR_HALF_TOP, CHAR_HALF_BOTTOM },
        { CHAR_EMPTY, CHAR_MIDDLE, CHAR_TOP_BOTTOM },
        { CHAR_HALF_BOTTOM, CHAR_HALF_BOTTOM, CHAR_HALF_TOP }
    },

    // 4
    {
        { CHAR_FULL, CHAR_EMPTY, CHAR_HALF_BOTTOM },
        { CHAR_FULL, CHAR_HALF_BOTTOM, CHAR_FULL },
        { CHAR_EMPTY, CHAR_EMPTY, CHAR_FULL }
    },

    // 5
    {
        { CHAR_FULL, CHAR_HALF_TOP, CHAR_HALF_TOP },
        { CHAR_HALF_TOP, CHAR_MIDDLE, CHAR_HALF_BOTTOM },
        { CHAR_HALF_BOTTOM, CHAR_HALF_BOTTOM, CHAR_BOTTOM_RIGHT }
    },

    // 6
    {
        { CHAR_TOP_LEFT, CHAR_HALF_TOP, CHAR_HALF_TOP },
        { CHAR_FULL, CHAR_HALF_TOP, CHAR_TOP_RIGHT },
        { CHAR_BOTTOM_LEFT, CHAR_HALF_BOTTOM, CHAR_BOTTOM_RIGHT }
    },

    // 7
    {
        { CHAR_HALF_TOP, CHAR_HALF_TOP, CHAR_FULL },
        { CHAR_EMPTY, CHAR_HALF_BOTTOM, CHAR_HALF_TOP },
        { CHAR_EMPTY, CHAR_FULL, CHAR_EMPTY }
    },

    // 8
    {
        { CHAR_HALF_BOTTOM, CHAR_HALF_TOP, CHAR_HALF_BOTTOM },
        { CHAR_TOP_BOTTOM, CHAR_MIDDLE, CHAR_TOP_BOTTOM },
        { CHAR_HALF_TOP, CHAR_HALF_BOTTOM, CHAR_HALF_TOP }
    },

    // 9
    {
        { CHAR_TOP_LEFT, CHAR_HALF_TOP, CHAR_TOP_RIGHT },
        { CHAR_BOTTOM_LEFT, CHAR_HALF_BOTTOM, CHAR_FULL },
        { CHAR_HALF_BOTTOM, CHAR_HALF_BOTTOM, CHAR_BOTTOM_RIGHT }
    }
};
```

Os n√∫meros que ser√£o criados com isso s√£o:

![N√∫meros de 1 a 5 no display][]
![N√∫meros de 6 a 9 no display][]

Isso finaliza a parte trabalhosa. Agora resta apenas l√≥gica.

## A l√≥gica de exibi√ß√£o dos n√∫meros

A primeira parte da l√≥gica come√ßa com a cria√ß√£o dos caracteres:

```cpp
LcdBigNumbers::LcdBigNumbers(LiquidCrystal_I2C *lcd)
{
    this->lcd = lcd;
}

void LcdBigNumbers::init()
{
    for (uint8_t i = 0; i < 8; i++) {
        this->lcd->createChar(i, customChars[i]);
    }
}
```

O m√©todo `init()` ir√° percorrer todo o vetor de caracteres customizados, criando
todos eles no controlador do LCD. O primeiro par√¢metro do `createChar()` √© o
endere√ßo que o o caractere deve ocupar na mem√≥ria, e o segundo √© um vetor com os
bytes que foram o padr√£o de exibi√ß√£o.

O m√©todo `printNumber()` √© utilizado para decompor um n√∫mero em v√°rios d√≠gitos e
depois exibir cada d√≠gito separadamente, com um pequeno espa√ßo entre eles. O
m√©todo recebe por par√¢metro a coluna e a linha do primeiro "pixel" (topo esquerdo)
onde ser√° exibido o n√∫mero.

```cpp
void LcdBigNumbers::printNumber(
    const unsigned long int number,
    uint8_t column,
    uint8_t row
)
{
    uint8_t digits[10];  // Hard limit de 10 d√≠gitos
    uint8_t count = 0, i;
    unsigned long int moreNumbers = number;

    do {
        digits[count++] = moreNumbers % 10;
        moreNumbers /= 10;
    } while (count < 10 && moreNumbers > 0);

    for (i = 0; i < count; i++) {
        this->printDigit(digits[count - i - 1], column + 4 * i, row);
    }
}
```

**Nota: revisando o c√≥digo acima durante a atualiza√ß√£o do blog para o empress,
encontrei uma pequena grande falha de _[buffer overflow][]_ na condi√ß√£o do
`while`: `count <= 10` deveria ser `count < 10`. O c√≥digo acima j√° est√° corrigido.**

Esse talvez seja o c√≥digo mais complexo em todo o projeto at√© agora, mas at√© que
√© simples. O n√∫mero recebido vai sendo dividido por 10 at√© chegar a zero, e o
resto da divis√£o (que √© o mesmo que o √∫ltimo d√≠gito do n√∫mero) √© adicionado a
uma lista. No fim, essa lista ter√° todos os d√≠gitos do n√∫mero em ordem inversa.

Depois de decompor o n√∫mero, a lista de d√≠gitos √© percorrida do final para o come√ßo
e, para cada d√≠gito, √© chamado o outro m√©todo da biblioteca, o `printDigit()`.
Para cada d√≠gito exibido, s√£o avan√ßadas 4 colunas para exibir o pr√≥ximo. Como
cada n√∫mero sempre ocupa 3 colunas, isso deixa uma coluna de espa√ßo entre os
d√≠gitos (_e isso tamb√©m pode causar um pequeno bug, que n√£o afeta o Placarduino,
porque essa coluna de espa√ßo entre os d√≠gitos n√£o √© apagada, ent√£o se havia outra
coisa escrita nesse espa√ßo antes, ir√° continuar aparecendo entre os d√≠gitos_).

Por fim, a l√≥gica para exibir um d√≠gito no display LCD. Como a defini√ß√£o de quais
caracteres formam o n√∫mero j√° foi feita antes na matriz `numberChars`, o √∫nico
trabalho desse m√©todo √© percorrer essa matriz, posicionar o cursor e exibir o
caractere:

```cpp
void LcdBigNumbers::printDigit(const uint8_t digit, uint8_t column, uint8_t row)
{
    if (digit > 9) {
        return;
    }

    uint8_t r, c;

    for (r = 0; r < 3; r++) {
        this->lcd->setCursor(column, row + r);

        for (c = 0; c < 3; c++) {
            this->lcd->write(numberChars[digit][r][c]);
        }
    }
}
```

## Integrando o LcdBigNumbers com o Placarduino

O uso da biblioteca para exibir os pontos dos jogadores no placar vai simplificar
a fun√ß√£o `printScore()` atual. Mas, primeiro, a biblioteca deve ser inclu√≠da e
inicializada.

No come√ßo do arquivo, depois dos outros `#include`'s:

```cpp
#include "LcdBigNumbers.h"
```

Um objeto LcdBigNumbers deve ser criado logo depois do LiquidCrystal_I2C, passando
um ponteiro para a vari√°vel `lcd`:

```
LiquidCrystal_I2C lcd(LCD_I2C_ADDR, LCD_COLS, LCD_ROWS);
LcdBigNumbers bigNumbers(&lcd);
```

O m√©todo `init()`, que ir√° criar todos os caracteres customizados, deve ser
chamado logo ap√≥s a inicializa√ß√£o do LCD:

```cpp
void setupLCD()
{
    lcd.init();
    bigNumbers.init();

    lcd.backlight();
}
```

Por fim, a fun√ß√£o `printScore()` √© alterada para exibir os nomes dos dois
jogadores na primeira linha, um alinhado √† esquerda e outro √† direita. A
exibi√ß√£o dos nomes ainda √© feita utilizando `lcd.print()`, mas a exibi√ß√£o dos
pontos agora √© feita com `bigNumbers.printNumber()`, alinhando, como os nomes,
√† esquerda e √† direita.

```cpp
void printScore()
{
    lcd.clear();

    lcd.setCursor(0, 0);
    lcd.print(firstPlayerName);

    // Alinha o nome do jogador √† direita
    lcd.setCursor(LCD_COLS - strlen(secondPlayerName), 0);
    lcd.print(secondPlayerName);

    bigNumbers.printNumber(firstPlayerScore, 0, 1);

    // Altera coluna inicial de acordo com valor da pontua√ß√£o para
    // manter alinhado √† direita
    if (secondPlayerScore >= 10) {
        bigNumbers.printNumber(secondPlayerScore, LCD_COLS - 7, 1);
    }
    else {
        bigNumbers.printNumber(secondPlayerScore, LCD_COLS - 3, 1);
    }
}
```

## Resultado

![N√∫meros grandes no Placarduino][]

Como sempre, o c√≥digo est√° no GitHub:

- A vers√£o desse post: https://github.com/eduardoweiland/placarduino/tree/v1.3
- Vers√£o mais atual: https://github.com/eduardoweiland/placarduino


[√∫ltimo post sobre o projeto do Placarduino]: /2017-11-02-placarduino-adicionando-um-display-maior-para-exibir-mais-informacoes
[estrutura do PlatformIO]: /2017-10-31-substituindo-a-ide-do-arduino-pelo-platformio
[N√∫meros grandes no Placarduino]: /images/placarduino-big-numbers-lcd.jpg
[Representa√ß√£o da letra E no display LCD]: /images/display-lcd-letra-E.png
[Representa√ß√£o em bits da letra E no display LCD]: /images/display-lcd-letra-E-bits.png
[Todos os caracteres utilizados na biblioteca LcdBigNumbers]: /images/custom-chars-lcd-big-number.png
[N√∫meros de 1 a 5 no display]: /images/lcd-big-numbers-1-a-5.jpg
[N√∫meros de 6 a 9 no display]: /images/lcd-big-numbers-6-a-9.jpg
[post-engineersgarage]: https://www.engineersgarage.com/embedded/arduino/how-to-create-custom-characters-in-lcd-using-arduino
[gerador de caracteres customizados]: https://omerk.github.io/lcdchargen/
[Dependency Injection]: https://en.wikipedia.org/wiki/Dependency_injection
[buffer overflow]: https://pt.wikipedia.org/wiki/Transbordamento_de_dados
